plugins {
  id 'com.gradle.enterprise' version '3.11.3'
}

include ':acceptance-tests'
include ':configuration-maven-extension'

rootProject.name = 'gradle-plugin'

if (!JavaVersion.current().java11) {
  throw new GradleException('Build requires Java 11')
}

gradle.ext.ciBuild = System.getenv()['JENKINS_URL'] ? true : false

gradleEnterprise {
  buildScan {
    if (gradle.ciBuild) {
      tag('CI')
      ifNonNull(System.getenv('GIT_BRANCH')) { tag(it) }
      ifNonNull(System.getenv('GIT_COMMIT')) { value('git-commit-id', it) }
      ifNonNull(System.getenv('BUILD_URL')) { link('Jenkins build', it) }

      termsOfServiceUrl = 'https://gradle.com/terms-of-service'
      termsOfServiceAgree = 'yes'
      publishAlways()
    } else {
      tag 'LOCAL'
    }
  }
}

static void ifNonNull(value, Closure action) {
  if (value) {
    action.call(value)
  }
}

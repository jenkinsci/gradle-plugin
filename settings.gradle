plugins {
  id 'com.gradle.enterprise' version '3.11.3'
  id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.8.2'
}

include ':acceptance-tests'
include ':configuration-maven-extension'

rootProject.name = 'gradle-plugin'

if (!JavaVersion.current().java11) {
  throw new GradleException('Build requires Java 11')
}

gradle.ext.ciJenkinsBuild = System.getenv('JENKINS_URL') ? true : false
gradle.ext.ciTeamCityBuild = System.getenv('TEAMCITY_VERSION') ? true : false
gradle.ext.isCi = gradle.ciJenkinsBuild || gradle.ciTeamCityBuild

gradleEnterprise {
  buildScan {
    capture { taskInputFiles = true }
    uploadInBackground = !gradle.isCi
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
    obfuscation {
      ipAddresses { addresses -> addresses.collect { address -> "0.0.0.0"} }
    }
  }
}

buildCache {
    local {
        enabled = true
    }
    if (gradle.ciTeamCityBuild) {
        remote(gradleEnterprise.buildCache) {
            enabled = true
            push = true
            server = System.getenv('GRADLE_CACHE_REMOTE_URL')
        }
    }
}

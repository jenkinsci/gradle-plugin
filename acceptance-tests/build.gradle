import org.gradle.internal.os.OperatingSystem

import java.util.regex.Pattern

plugins {
    id 'java'
    id "de.undercouch.download" version "5.3.0"
}

ext {
    athVersion = '5460.va_c8a_323c73b_a'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

group = 'org.jenkins-ci.plugins'
description = 'Acceptance tests of Gradle plugin'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    gradlePlugin {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    // same version as used by ATH
    annotationProcessor "org.jenkins-ci:annotation-indexer:1.12"

    implementation "org.jenkins-ci:acceptance-test-harness:${athVersion}"

    testImplementation platform("io.netty:netty-bom:4.1.85.Final")
    testImplementation "io.ratpack:ratpack-test:1.9.0"

    gradlePlugin project(path: ':', configuration: 'gradlePluginJpi')
}

def currentJava = JavaVersion.current()

def jenkinsVersions = [
    JenkinsVersion.LATEST,
    JenkinsVersion.LATEST_LTS,
    JenkinsVersion.V2_356
]

def allTestTasks =
    jenkinsVersions
        .findAll { jenkinsVersion ->
            jenkinsVersion.default || currentJava.isCompatibleWith(jenkinsVersion.requiredJavaVersion)
        }
        .collect { jenkinsVersion ->
            def downloadJenkinsTask =
                tasks.register("downloadJenkins${jenkinsVersion.label}", Download) {
                    src jenkinsVersion.downloadUrl
                    dest file("${project.gradle.gradleUserHomeDir}/jenkins-cache/${jenkinsVersion.version}/jenkins.war")
                    onlyIfModified true
                    tempAndMove true
                }

            def testTask =
                jenkinsVersion.default
                    ? tasks.named("test")
                    : tasks.register("test${jenkinsVersion.label}", Test)

            testTask.configure {
                def jenkinsWar = downloadJenkinsTask
                def gradlePlugin = configurations.gradlePlugin

                inputs.files(jenkinsWar)
                    .withPropertyName("jenkinsWar")
                    .withPathSensitivity(PathSensitivity.NAME_ONLY)
                inputs.files(gradlePlugin)
                    .withPropertyName("gradlePlugin")
                    .withPathSensitivity(PathSensitivity.RELATIVE)

                onlyIf {
                    // Do not run on Windows as written here: https://github.com/jenkinsci/acceptance-test-harness/blob/master/docs/EXTERNAL.md
                    !OperatingSystem.current().isWindows()
                }

                javaLauncher = javaToolchains.launcherFor {
                    languageVersion = jenkinsVersion.javaVersion
                }

                // If executed on TeamCity, we need to set the Chromedriver path
                if (gradle.ciTeamCityBuild) {
                    systemProperty 'webdriver.chrome.driver', "${System.getenv('HOME')}/.gradle/webdriver/chromedriver/chromedriver"
                }

                doFirst {
                    environment([
                        JENKINS_WAR: jenkinsWar.get().outputs.files.singleFile,
                        LOCAL_JARS : gradlePlugin.singleFile,
                        BROWSER    : gradle.ciJenkinsBuild ? 'firefox-container' : 'chrome'
                    ])
                }
            }

            testTask
        }

def testAllTask = tasks.register("testAll") {
    dependsOn allTestTasks
}

tasks.named("check").configure {
    dependsOn testAllTask
}

class JenkinsVersion {

    private static final String LATEST_VERSION = "latest"
    private static final String LATEST_LTS_VERSION = "latest-lts"
    private static final String V2_356_VERSION = '2.356'

    private static final Pattern JENKINS_VERSION_PATTERN = ~"^\\d+([.]\\d+)*?\$"

    private static final JavaLanguageVersion JAVA_11 = JavaLanguageVersion.of(11)

    public static final JenkinsVersion LATEST = of(LATEST_VERSION)
    public static final JenkinsVersion LATEST_LTS = of(LATEST_LTS_VERSION)
    public static final JenkinsVersion V2_356 = of(V2_356_VERSION)

    private static final String MIRROR = 'https://get.jenkins.io'

    final String version
    final URL downloadUrl
    final JavaLanguageVersion javaVersion

    private JenkinsVersion(String version, URL downloadUrl, JavaLanguageVersion javaVersion) {
        this.version = version
        this.downloadUrl = downloadUrl
        this.javaVersion = javaVersion
    }

    boolean isDefault() {
        return version == V2_356_VERSION
    }

    String getLabel() {
        return isJenkinsVersion(version)
            ? version.replaceAll('\\.', '_')
            : version.split('-').collect { it.capitalize() }.join()
    }

    JavaVersion getRequiredJavaVersion() {
        return JavaVersion.toVersion(javaVersion.toString())
    }

    static JenkinsVersion of(String version, JavaLanguageVersion javaVersion = JAVA_11) {
        String downloadUrl
        if (version == LATEST_VERSION) {
            downloadUrl = "${MIRROR}/war/latest/jenkins.war"
        } else if (version == 'latest-lts') {
            downloadUrl = "${MIRROR}/war-stable/latest/jenkins.war"
        } else {
            if (!isJenkinsVersion(version)) {
                throw new GradleException("Unsupported Jenkins version '${version}'")
            }
            downloadUrl = "https://repo.jenkins-ci.org/public/org/jenkins-ci/main/jenkins-war/${version}/jenkins-war-${version}.war"
        }

        return new JenkinsVersion(version, new URL(downloadUrl), javaVersion)
    }

    private static boolean isJenkinsVersion(String version) {
        return version ==~ JENKINS_VERSION_PATTERN
    }
}
